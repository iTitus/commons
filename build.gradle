plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

group = 'io.github.ititus'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }

    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.7.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.18.0'
    testImplementation 'org.mockito:mockito-core:3.6.0'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.property('bintray_user')
    key = project.property('bintray_api_key')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'ititus-commons'
        name = 'commons'
        version {
            name = project.version
        }
    }
}
